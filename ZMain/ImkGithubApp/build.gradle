apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

// apply plugin: 'AndResGuard'

// apply plugin: 'blockcanaryex'

def myVersionCode = 1
def myVersionName = "0.0.1"
def myVersionNameSuffix = ".0"

android {
    compileSdkVersion androidBuild.compileSdkVersion
    buildToolsVersion androidBuild.buildToolsVersion

    defaultConfig {
        applicationId "net.imknown.imkgithub"

        minSdkVersion androidBuild.minSdkVersion
        targetSdkVersion androidBuild.targetSdkVersionForNoRuntimePermission

        versionCode myVersionCode
        versionName myVersionName

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        // region [Packaging]
        // setProperty("archivesBaseName", "MyGithub-$versionName")
        archivesBaseName = 'MyGithub'

        manifestPlaceholders = [
                BAIDU_APP_KEY: 'xxx',

                UMENG_APP_KEY: "xxx",

                JPUSH_PKGNAME: applicationId,
                JPUSH_APPKEY : "xxx", //JPush上注册的包名对应的appkey.
                JPUSH_CHANNEL: "developer-default", //暂时填写默认值即可.
        ]
        // endregion
    }

    signingConfigs {
        release {
            storeFile file(keysConfig.release.storeFile)
            storePassword keysConfig.release.storePassword
            keyAlias keysConfig.release.keyAlias
            keyPassword keysConfig.release.keyPassword
        }

        debug {
            storeFile file(keysConfig.debug.storeFile)
            storePassword keysConfig.debug.storePassword
            keyAlias keysConfig.debug.keyAlias
            keyPassword keysConfig.debug.keyPassword
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true

            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    "$rootDir/ZOthers/proguard/proguard-rules-base.pro",
                    'proguard-rules.pro'
            // local.properties: ${sdk.dir}/tools/proguard/proguard-android.txt

            signingConfig signingConfigs.release

            ndk {
                abiFilters 'armeabi-v7a', 'arm64-v8a'
            }

            manifestPlaceholders = [UMENG_APP_KEY: "xxx"]
        }

        debug {
            debuggable true
            jniDebuggable true

            signingConfig signingConfigs.debug

            versionNameSuffix myVersionNameSuffix

            ndk {
                abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
            }
        }

//        buildTypes.each {
//            it.buildConfigField 'boolean', 'isProductEnv', is_product_env
//
//            it.buildConfigField 'String', 'urlProduct', url_product
//            it.buildConfigField 'String', 'urlProductImage', url_product_image
//            it.buildConfigField 'String', 'urlTest', url_test
//            it.buildConfigField 'String', 'urlTestImage', url_test_image
//        }
    }

//    applicationVariants.all { variant ->
//        renameAPKs(variant, defaultConfig, buildType, signingConfig, productFlavors);
//    }

    // region [Flavors]
    flavorDimensions flavorDimensionsConfig.default

    productFlavors {
        AOfficial {
            // 官方
            dimension flavorDimensionsConfig.default
        }

        BOthers {
            dimension flavorDimensionsConfig.default
        }
    }

    productFlavors.all {
        flavor -> flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
    }
    // endregion

    sourceSets {
        main {
            // jniLibs.srcDirs = ['libs']

            res.srcDirs = [
                    'src/main/res',

                    'src/main/java/net/imknown/imkgithub/ui/register/',
                    'src/main/java/net/imknown/imkgithub/ui/login/',
            ]
        }
    }
}

dependencies {
    apply from: "$rootDir/ZOthers/gradle/testDependencies.gradle"

    // region [Files]
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':ZMainLib:ImkCode')
    // endregion

    // region [Support]
    implementation "com.android.support:appcompat-v7:$supportLibs.supportVersion"
    implementation "com.android.support:design:$supportLibs.supportVersion"
    implementation "com.android.support.constraint:constraint-layout:$supportLibs.constraintVersion"
    // endregion

    // region [Debug]
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$thirdParties.leakCanary"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$thirdParties.leakCanary"
    testImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$thirdParties.leakCanary"

    debugImplementation "com.github.markzhai:blockcanary-android:$thirdParties.blockCanary"
    releaseImplementation "com.github.markzhai:blockcanary-no-op:$thirdParties.blockCanary"
    testImplementation "com.github.markzhai:blockcanary-no-op:$thirdParties.blockCanary"

    debugImplementation "com.letv.sarrsdesktop:BlockCanaryExJRT:$thirdParties.blockCanaryEx"
    releaseImplementation "com.letv.sarrsdesktop:BlockCanaryExJRTNoOp:$thirdParties.blockCanaryEx"
    testImplementation "com.letv.sarrsdesktop:BlockCanaryExJRTNoOp:$thirdParties.blockCanaryEx"

    // implementation 'im.fir:fir-sdk:latest.integration@aar'
    // endregion

    // region [DI]
    implementation "com.jakewharton:butterknife:$thirdParties.butterKnife"
    annotationProcessor "com.jakewharton:butterknife-compiler:$thirdParties.butterKnife"

    implementation "com.google.dagger:dagger:$thirdParties.dagger2"
    annotationProcessor "com.google.dagger:dagger-compiler:$thirdParties.dagger2"
    // endregion

    // region [Tools]
    implementation "com.squareup.retrofit2:retrofit:$thirdParties.retrofit2"
    implementation "com.squareup.retrofit2:converter-gson:$thirdParties.retrofit2"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$thirdParties.retrofit2"
    implementation "com.google.code.gson:gson:$thirdParties.gson"

    implementation "io.reactivex.rxjava2:rxjava:$thirdParties.rxJava"
    implementation "io.reactivex.rxjava2:rxandroid:$thirdParties.rxAndroid"
    implementation "io.reactivex.rxjava2:rxkotlin:$thirdParties.rxKotlin"

    implementation "com.github.bumptech.glide:glide:$thirdParties.glide"
    annotationProcessor "com.github.bumptech.glide:compiler:$thirdParties.glide"
    // endregion
}
